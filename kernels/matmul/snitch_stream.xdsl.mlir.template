riscv.assembly_section ".text" {
  riscv.directive ".globl" "matmul"
  riscv.directive ".p2align" "2"

// x[ M x K ]
// y[ K x N ]
// g[ M x N ]
  riscv_func.func @matmul(
    %X : !riscv.reg<a0>,
    %Y : !riscv.reg<a1>,
    %G : !riscv.reg<a2>
  ) {
    %X_moved = riscv.mv %X : (!riscv.reg<a0>) -> !riscv.reg<>
    %Y_moved = riscv.mv %Y : (!riscv.reg<a1>) -> !riscv.reg<>
    %G_moved = riscv.mv %G : (!riscv.reg<a2>) -> !riscv.reg<>

    %c0 = riscv.get_register : () -> !riscv.reg<zero>
    %c1 = riscv.li 1 : () -> !riscv.reg<>
    %c4 = riscv.li 4 : () -> !riscv.reg<>
    %c32 = riscv.li 32 : () -> !riscv.reg<>
    %frep_count_minus_one = riscv.li {{K - 1}} : () -> !riscv.reg<>
    %target_count = riscv.li {{M * N}} : () -> !riscv.reg<>

    %zero_float = riscv.fcvt.d.w %c0 : (!riscv.reg<zero>) -> !riscv.freg<>

    %G_begin = riscv.mv %G_moved : (!riscv.reg<>) -> !riscv.reg<>
    %G_end0 = riscv.addi %G_begin, {{min(M * N * 8, 2047)}} : (!riscv.reg<>) -> !riscv.reg<>
    %G_end1 = riscv.addi %G_end0, {{max(M * N * 8 - 2047, 0)}} : (!riscv.reg<>) -> !riscv.reg<>

    "snitch_stream.streaming_region"(%X_moved, %Y_moved, %G_moved) <{
      "stride_patterns" = [
        #snitch_stream.stride_pattern<ub = [{{M}}, {{N // 4}}, {{K}}, 4], strides = [{{K * 8}}, 0, 8, 0]>,
        #snitch_stream.stride_pattern<ub = [{{M}}, {{N // 4}}, {{K}}, 4], strides = [0, 32, {{N * 8}}, 8]>
      ],
      "operandSegmentSizes" = array<i32: 2, 0>
    }> ({
    ^bb0(%X_stream : !stream.readable<!riscv.freg<ft0>>, %Y_stream : !stream.readable<!riscv.freg<ft1>>):
      riscv_scf.for %g_i : !riscv.reg<> = %G_begin to %G_end1 step %c32 {
        %init0 = riscv.fmv.d %zero_float : (!riscv.freg<>) -> !riscv.freg<>
        %init1 = riscv.fmv.d %zero_float : (!riscv.freg<>) -> !riscv.freg<>
        %init2 = riscv.fmv.d %zero_float : (!riscv.freg<>) -> !riscv.freg<>
        %init3 = riscv.fmv.d %zero_float : (!riscv.freg<>) -> !riscv.freg<>

        %g00, %g01, %g02, %g03 = riscv_snitch.frep_outer %frep_count_minus_one iter_args(%acc0 = %init0, %acc1 = %init1, %acc2 = %init2, %acc3 = %init3) -> (!riscv.freg<>, !riscv.freg<>, !riscv.freg<>, !riscv.freg<>) {
          %x10 = riscv_snitch.read from %X_stream : !riscv.freg<ft0>
          %y10 = riscv_snitch.read from %Y_stream : !riscv.freg<ft1>
          %res0 = riscv.fmadd.d %x10, %y10, %acc0 : (!riscv.freg<ft0>, !riscv.freg<ft1>, !riscv.freg<>) -> !riscv.freg<>
          %x11 = riscv_snitch.read from %X_stream : !riscv.freg<ft0>
          %y11 = riscv_snitch.read from %Y_stream : !riscv.freg<ft1>
          %res1 = riscv.fmadd.d %x11, %y11, %acc1 : (!riscv.freg<ft0>, !riscv.freg<ft1>, !riscv.freg<>) -> !riscv.freg<>
          %x12 = riscv_snitch.read from %X_stream : !riscv.freg<ft0>
          %y12 = riscv_snitch.read from %Y_stream : !riscv.freg<ft1>
          %res2 = riscv.fmadd.d %x12, %y12, %acc2 : (!riscv.freg<ft0>, !riscv.freg<ft1>, !riscv.freg<>) -> !riscv.freg<>
          %x13 = riscv_snitch.read from %X_stream : !riscv.freg<ft0>
          %y13 = riscv_snitch.read from %Y_stream : !riscv.freg<ft1>
          %res3 = riscv.fmadd.d %x13, %y13, %acc3 : (!riscv.freg<ft0>, !riscv.freg<ft1>, !riscv.freg<>) -> !riscv.freg<>

          riscv_snitch.frep_yield %res0, %res1, %res2, %res3 : !riscv.freg<>, !riscv.freg<>, !riscv.freg<>, !riscv.freg<>
        }

        riscv.fsd %g_i, %g00, 0 : (!riscv.reg<>, !riscv.freg<>) -> ()
        riscv.fsd %g_i, %g01, 8 : (!riscv.reg<>, !riscv.freg<>) -> ()
        riscv.fsd %g_i, %g02, 16 : (!riscv.reg<>, !riscv.freg<>) -> ()
        riscv.fsd %g_i, %g03, 24 : (!riscv.reg<>, !riscv.freg<>) -> ()

        riscv_scf.yield
      }
    }) : (!riscv.reg<>, !riscv.reg<>, !riscv.reg<>) -> ()

    riscv_func.return
  }
}
