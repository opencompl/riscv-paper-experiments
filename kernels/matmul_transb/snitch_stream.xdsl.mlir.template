riscv.assembly_section ".text" {
  riscv.directive ".globl" "matmul_transb"
  riscv.directive ".p2align" "2"

// x[ M x K ]
// y[ N X K ]
// g[ M x N ]
  riscv_func.func @matmul_transb(
    %X : !riscv.reg<a0>,
    %Y : !riscv.reg<a1>,
    %G : !riscv.reg<a2>
  ) {
    %X_moved = riscv.mv %X : (!riscv.reg<a0>) -> !riscv.reg
    %Y_moved = riscv.mv %Y : (!riscv.reg<a1>) -> !riscv.reg
    %G_moved = riscv.mv %G : (!riscv.reg<a2>) -> !riscv.reg

    %zero = riscv.get_register : !riscv.reg<zero>
    %zero_float = riscv.fmv.w.x %zero : (!riscv.reg<zero>) -> !riscv.freg

    snitch_stream.streaming_region {
      stride_patterns = [
        #snitch_stream.stride_pattern<ub = [{{M}}, {{N // 4}}, {{K // 2}}], strides = [{{K * 4}}, 0, 8], repeat = 4>,
        #snitch_stream.stride_pattern<ub = [{{M}}, {{N // 4}}, {{K // 2}}, {{4}}], strides = [0, {{4 * 4 * K}}, {{4 * 2}}, {{4 * K}}]>
      ]
    } ins(%X_moved, %Y_moved : !riscv.reg, !riscv.reg) {
    ^bb0(%X_stream : !stream.readable<!riscv.freg<ft0>>, %Y_stream : !stream.readable<!riscv.freg<ft1>>):
      %res_size = riscv.li {{M * N * 4}} : !riscv.reg
      %row_size = riscv.li {{N * 4}} : !riscv.reg
      %row_tile_stride = riscv.li {{4 * 4}} : !riscv.reg
      %frep_count = riscv.li {{K // 2 - 1}} : !riscv.reg
      riscv_scf.for %row_offset : !riscv.reg = %zero to %res_size step %row_size {
        %row_start = riscv.add %G_moved, %row_offset : (!riscv.reg, !riscv.reg) -> !riscv.reg
        riscv_scf.for %row_tile_offset : !riscv.reg = %zero to %row_size step %row_tile_stride {
            %tile_start = riscv.add %row_start, %row_tile_offset : (!riscv.reg, !riscv.reg) -> !riscv.reg

            %g00 = riscv_snitch.vfcpka.s.s %zero_float, %zero_float : (!riscv.freg, !riscv.freg) -> !riscv.freg
            %g01 = riscv_snitch.vfcpka.s.s %zero_float, %zero_float : (!riscv.freg, !riscv.freg) -> !riscv.freg
            %g02 = riscv_snitch.vfcpka.s.s %zero_float, %zero_float : (!riscv.freg, !riscv.freg) -> !riscv.freg
            %g03 = riscv_snitch.vfcpka.s.s %zero_float, %zero_float : (!riscv.freg, !riscv.freg) -> !riscv.freg

            %x00 = riscv_snitch.read from %X_stream : !riscv.freg<ft0>
            %y00 = riscv_snitch.read from %Y_stream : !riscv.freg<ft1>
            %init0 = riscv.vfmul.s %x00, %y00 : (!riscv.freg<ft0>, !riscv.freg<ft1>) -> !riscv.freg
            %x01 = riscv_snitch.read from %X_stream : !riscv.freg<ft0>
            %y01 = riscv_snitch.read from %Y_stream : !riscv.freg<ft1>
            %init1 = riscv.vfmul.s %x01, %y01 : (!riscv.freg<ft0>, !riscv.freg<ft1>) -> !riscv.freg
            %x02 = riscv_snitch.read from %X_stream : !riscv.freg<ft0>
            %y02 = riscv_snitch.read from %Y_stream : !riscv.freg<ft1>
            %init2 = riscv.vfmul.s %x02, %y02 : (!riscv.freg<ft0>, !riscv.freg<ft1>) -> !riscv.freg
            %x03 = riscv_snitch.read from %X_stream : !riscv.freg<ft0>
            %y03 = riscv_snitch.read from %Y_stream : !riscv.freg<ft1>
            %init3 = riscv.vfmul.s %x03, %y03 : (!riscv.freg<ft0>, !riscv.freg<ft1>) -> !riscv.freg

            %frep_count_minus_one = riscv.addi %frep_count, -1 : (!riscv.reg) -> !riscv.reg

            %g10, %g11, %g12, %g13 = riscv_snitch.frep_outer %frep_count_minus_one iter_args(
                %acc0 = %init0, %acc1 = %init1, %acc2 = %init2, %acc3 = %init3
            ) -> (!riscv.freg, !riscv.freg, !riscv.freg, !riscv.freg) {
                %x10 = riscv_snitch.read from %X_stream : !riscv.freg<ft0>
                %y10 = riscv_snitch.read from %Y_stream : !riscv.freg<ft1>
                %res0 = riscv_snitch.vfmac.s %acc0, %x10, %y10 : (!riscv.freg, !riscv.freg<ft0>, !riscv.freg<ft1>) -> !riscv.freg
                %x11 = riscv_snitch.read from %X_stream : !riscv.freg<ft0>
                %y11 = riscv_snitch.read from %Y_stream : !riscv.freg<ft1>
                %res1 = riscv_snitch.vfmac.s %acc1, %x11, %y11 : (!riscv.freg, !riscv.freg<ft0>, !riscv.freg<ft1>) -> !riscv.freg
                %x12 = riscv_snitch.read from %X_stream : !riscv.freg<ft0>
                %y12 = riscv_snitch.read from %Y_stream : !riscv.freg<ft1>
                %res2 = riscv_snitch.vfmac.s %acc2, %x12, %y12 : (!riscv.freg, !riscv.freg<ft0>, !riscv.freg<ft1>) -> !riscv.freg
                %x13 = riscv_snitch.read from %X_stream : !riscv.freg<ft0>
                %y13 = riscv_snitch.read from %Y_stream : !riscv.freg<ft1>
                %res3 = riscv_snitch.vfmac.s %acc3, %x13, %y13 : (!riscv.freg, !riscv.freg<ft0>, !riscv.freg<ft1>) -> !riscv.freg

                riscv_snitch.frep_yield %res0, %res1, %res2, %res3 : !riscv.freg, !riscv.freg, !riscv.freg, !riscv.freg
            }


            %g20 = riscv_snitch.vfsum.s %g00, %g10 : (!riscv.freg, !riscv.freg) -> !riscv.freg
            %g21 = riscv_snitch.vfsum.s %g01, %g11 : (!riscv.freg, !riscv.freg) -> !riscv.freg
            %g22 = riscv_snitch.vfsum.s %g02, %g12 : (!riscv.freg, !riscv.freg) -> !riscv.freg
            %g23 = riscv_snitch.vfsum.s %g03, %g13 : (!riscv.freg, !riscv.freg) -> !riscv.freg

            riscv.fsw %tile_start, %g20, 0 : (!riscv.reg, !riscv.freg) -> ()
            riscv.fsw %tile_start, %g21, 4 : (!riscv.reg, !riscv.freg) -> ()
            riscv.fsw %tile_start, %g22, 8 : (!riscv.reg, !riscv.freg) -> ()
            riscv.fsw %tile_start, %g23, 12 : (!riscv.reg, !riscv.freg) -> ()

            riscv_scf.yield
        }

        riscv_scf.yield
      }
    }

    riscv_func.return
  }
}

